!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
!     #########
      SUBROUTINE DEF_VAR_NETCDF (DGU, &
                                 KFILE_ID,HNAME,HLONG_NAME,KDIM_ID,HATT_TITLE,HATT_TEXT,KVAR_ID,KTYPE,KLEN)
!
!!
!!    MODIFICATIONS
!!    -------------
!!      B. Decharme 07/2013 special case for time in netcdf output files
!-------------------------------------------------------------------------------
!
!
USE MODD_DIAG_SURF_ATM_n, ONLY : DIAG_SURF_ATM_t
!
USE MODD_IO_SURF_NC, ONLY : CFILEOUT_NC, NID_NC
USE MODD_OL_FILEID,      ONLY : XVAR_TO_FILEOUT, XOUT
USE MODD_SURF_PAR,       ONLY : XUNDEF, NUNDEF
!
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
INCLUDE "netcdf.inc"

!
TYPE(DIAG_SURF_ATM_t), INTENT(INOUT) :: DGU
!
INTEGER,               INTENT(IN) :: KFILE_ID
 CHARACTER(LEN=*),      INTENT(IN) :: HNAME
 CHARACTER(LEN=*),      INTENT(IN) :: HLONG_NAME
INTEGER, DIMENSION(:), INTENT(IN) :: KDIM_ID
 CHARACTER(LEN=*),DIMENSION(:),OPTIONAL, INTENT(IN) :: HATT_TITLE,HATT_TEXT
INTEGER, OPTIONAL, INTENT(OUT) :: KVAR_ID
INTEGER, OPTIONAL, INTENT(IN) :: KTYPE
INTEGER, OPTIONAL, INTENT(IN) :: KLEN
!
! ** local variables
!
 CHARACTER(LEN=20), DIMENSION(:), ALLOCATABLE :: YTEMP 
 CHARACTER(LEN=100) :: YNAME
 CHARACTER(LEN=50) :: YPAS
INTEGER, DIMENSION(4) :: IRET
INTEGER :: JRET,IVAR_ID,IATT,JATT,ILEN,INDIM
INTEGER :: IFIELD,JFIELD,ISIZE
LOGICAL :: NOWRITE, GOPENED, GEXIST
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!--------------------------------------
!
IF (LHOOK) CALL DR_HOOK('DEF_VAR_NETCDF',0,ZHOOK_HANDLE)
!
IF (.NOT.ALLOCATED(XVAR_TO_FILEOUT)) ALLOCATE(XVAR_TO_FILEOUT(100))
!
XOUT=XOUT+1
ALLOCATE(YTEMP(XOUT))
YTEMP(XOUT) = HNAME
IF (XOUT.GT.1) YTEMP(1:XOUT-1) = XVAR_TO_FILEOUT
!
IF(ALLOCATED(XVAR_TO_FILEOUT)) DEALLOCATE(XVAR_TO_FILEOUT)
ALLOCATE(XVAR_TO_FILEOUT(XOUT))
XVAR_TO_FILEOUT = YTEMP
!
DEALLOCATE(YTEMP)
!
! if output fields selection is active, test if this field is to be written
IF ( HNAME/='xx'  .AND. HNAME/='yy' .AND. HNAME/='lon' .AND. &
     HNAME/='lat' .AND. HNAME/='time' .AND. DGU%LSELECT )  THEN
  IFIELD=COUNT(DGU%CSELECT /= '            ')
  NOWRITE=.TRUE.
  DO JFIELD=1,IFIELD
    IF ( TRIM(DGU%CSELECT(JFIELD))==TRIM(HNAME) ) THEN
      NOWRITE=.FALSE.
    ENDIF
  ENDDO
  IF ( NOWRITE  .AND. LHOOK) CALL DR_HOOK('DEF_VAR_NETCDF',1,ZHOOK_HANDLE)
  IF ( NOWRITE ) RETURN
ENDIF
!
!define variables in the netcdf file
INDIM=SIZE(KDIM_ID)
!
IF (PRESENT(KTYPE)) THEN
  IRET(1) = NF_DEF_VAR(KFILE_ID,HNAME,KTYPE,INDIM,KDIM_ID,IVAR_ID)
ELSE
  IRET(1) = NF_DEF_VAR(KFILE_ID,HNAME,NF_DOUBLE,INDIM,KDIM_ID,IVAR_ID)
ENDIF
!
IF (INDIM/=0) THEN
  IF (PRESENT(KTYPE)) THEN
    IF (KTYPE==NF_DOUBLE .OR. KTYPE==NF_FLOAT) THEN
      IRET(2) = NF_PUT_ATT_DOUBLE (KFILE_ID,IVAR_ID,'_FillValue',NF_DOUBLE,1,XUNDEF)
    ELSEIF (KTYPE==NF_INT .OR. KTYPE==NF_SHORT) THEN
      IRET(2) = NF_PUT_ATT_INT (KFILE_ID,IVAR_ID,'_FillValue',NF_INT,1,NUNDEF)
    ELSEIF (KTYPE==NF_CHAR) THEN
      IF (INDIM>1) IRET(2) = NF_PUT_ATT_TEXT (KFILE_ID,IVAR_ID,'_FillValue',NF_CHAR,1,"")
      IF (PRESENT(KLEN)) IRET(3) = NF_PUT_ATT_INT (KFILE_ID,IVAR_ID,'len',NF_INT,1,KLEN)
    ENDIF
  ELSE
    IRET(2) = NF_PUT_ATT_DOUBLE (KFILE_ID,IVAR_ID,'_FillValue',NF_DOUBLE,1,XUNDEF)
  ENDIF
ENDIF
!
IF (PRESENT(KTYPE)) THEN
  IF (KTYPE==NF_CHAR) THEN
    IF (PRESENT(KLEN)) IRET(3) = NF_PUT_ATT_INT (KFILE_ID,IVAR_ID,'len',NF_INT,1,KLEN)
  ENDIF
ENDIF
!
IF (HLONG_NAME.NE.'') IRET(2) = NF_PUT_ATT_TEXT (KFILE_ID,IVAR_ID,'long_name',LEN_TRIM(HLONG_NAME),HLONG_NAME)
!
!Write optional attribute
IF (PRESENT(HATT_TITLE).AND.PRESENT(HATT_TEXT)) THEN  
  IATT=SIZE(HATT_TITLE)
  IF (IATT .EQ. SIZE(HATT_TEXT)) THEN
    DO JATT=1,IATT
      ILEN=LEN_TRIM(HATT_TEXT(JATT))
      YPAS=HATT_TEXT(JATT)
      JRET = NF_PUT_ATT_TEXT (KFILE_ID,IVAR_ID,HATT_TITLE(JATT),ILEN,HATT_TEXT(JATT))
    ENDDO
  ENDIF
ENDIF
!
IF (PRESENT(KVAR_ID)) KVAR_ID = IVAR_ID
!
IF (LHOOK) CALL DR_HOOK('DEF_VAR_NETCDF',1,ZHOOK_HANDLE)
!
END SUBROUTINE DEF_VAR_NETCDF
