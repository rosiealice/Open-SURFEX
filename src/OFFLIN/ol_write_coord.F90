!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
SUBROUTINE OL_WRITE_COORD (DGU, &
                           HFILE, KFILE_ID, KDDIM, HATT_TITLE, HNAME_DIM, &
                          HUNIT1, HUNIT2, KDIM1, HDATE, PX, PY)
!
!
!
USE MODD_DIAG_SURF_ATM_n, ONLY : DIAG_SURF_ATM_t
!
USE MODD_OL_FILEID,   ONLY : XID, XOUT
USE MODN_IO_OFFLINE,  ONLY : LWRITE_COORD
!
USE MODI_DEF_VAR_NETCDF
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
include 'netcdf.inc'
!
!
TYPE(DIAG_SURF_ATM_t), INTENT(INOUT) :: DGU
!
 CHARACTER(LEN=50),     INTENT(IN) :: HFILE
INTEGER,               INTENT(IN) :: KFILE_ID
INTEGER, DIMENSION(:), INTENT(IN) :: KDDIM
 CHARACTER(LEN=100), DIMENSION(:), INTENT(IN) :: HATT_TITLE
 CHARACTER(LEN=100), DIMENSION(:), INTENT(IN) :: HNAME_DIM
 CHARACTER(LEN=13) , DIMENSION(:), INTENT(IN) :: HUNIT1, HUNIT2
INTEGER, INTENT(IN) :: KDIM1
 CHARACTER(LEN=40),DIMENSION(:), INTENT(IN) :: HDATE
REAL,DIMENSION(:), INTENT(IN)              :: PX, PY
!
INTEGER, DIMENSION(:), ALLOCATABLE :: ITEMP
INTEGER :: IVAR_ID, JRET
INTEGER :: INDIMS
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('OL_WRITE_COORD',0,ZHOOK_HANDLE)
!
IF ( LWRITE_COORD ) THEN
  CALL DEF_VAR_NETCDF(DGU, &
                      KFILE_ID,'xx','abscissas',KDDIM(1:1),HATT_TITLE,(/''/))
  CALL DEF_VAR_NETCDF(DGU, &
                      KFILE_ID,'yy','ordinates',KDDIM(1:1),HATT_TITLE,(/''/))
ELSEIF (KDIM1.NE.0) THEN
  CALL DEF_VAR_NETCDF(DGU, &
                      KFILE_ID,TRIM(HNAME_DIM(1)),'',KDDIM(1:1),HATT_TITLE,HUNIT1)
  CALL DEF_VAR_NETCDF(DGU, &
                      KFILE_ID,TRIM(HNAME_DIM(2)),'',KDDIM(2:2),HATT_TITLE,HUNIT2)
ENDIF
INDIMS = SIZE(KDDIM)
 CALL DEF_VAR_NETCDF(DGU, &
                      KFILE_ID,'time','',KDDIM(INDIMS:INDIMS),HATT_TITLE,HDATE)
!
JRET=NF_ENDDEF(KFILE_ID)
!
JRET=NF_OPEN(HFILE, NF_WRITE,KFILE_ID)
IF (KDIM1.NE.0) THEN
  JRET = NF_INQ_VARID      (KFILE_ID, HNAME_DIM(1), IVAR_ID)
  JRET = NF_PUT_VAR_DOUBLE (KFILE_ID, IVAR_ID, PX)
  JRET = NF_INQ_VARID      (KFILE_ID, HNAME_DIM(2), IVAR_ID)
  JRET = NF_PUT_VAR_DOUBLE (KFILE_ID, IVAR_ID, PY)
ELSEIF (LWRITE_COORD) THEN
  JRET = NF_INQ_VARID      (KFILE_ID, 'xx', IVAR_ID)
  JRET = NF_PUT_VAR_DOUBLE (KFILE_ID, IVAR_ID, PX)
  JRET = NF_INQ_VARID      (KFILE_ID, 'yy', IVAR_ID)
  JRET = NF_PUT_VAR_DOUBLE (KFILE_ID, IVAR_ID, PY)
ENDIF
!
ALLOCATE(ITEMP(XOUT))
ITEMP(SIZE(XID)+1:XOUT) = KFILE_ID
IF (SIZE(XID).GT.0) ITEMP(1:SIZE(XID))=XID
DEALLOCATE(XID)
ALLOCATE(XID(XOUT))
XID=ITEMP
DEALLOCATE(ITEMP)
!
IF (LHOOK) CALL DR_HOOK('OL_WRITE_COORD',1,ZHOOK_HANDLE)
!
END SUBROUTINE OL_WRITE_COORD
